#Использовать gui

Перем Форма;
Перем ВерсииOneScript;
Перем УправляемыйИнтерфейс;

#Область Элементы_формы

Перем СтраницаУстановленныеВерсии;
Перем СтраницаДоступныеВерсии;

Перем УстановленныеВерсии;
Перем ДоступныеВерсии;

#КонецОбласти

#Область Реквизиты_формы

Перем ТаблицаУстановленныеВерсии;
Перем ТаблицаДоступныеВерсии;

#КонецОбласти

#Область События_формы

Процедура Открыть() Экспорт

	ОтобразитьСписокУстановленныхВерсий();
	ОтобразитьСписокДоступныхВерсий();
	Форма.Показать();

КонецПроцедуры

#КонецОбласти

#Область Обработчики_нажатия_кнопок

Процедура ПриНажатииУстановленныеВерсииОбновить() Экспорт
	
	ТаблицаУстановленныеВерсии.Источник = ВерсииOneScript.ПолучитьСписокУстановленныхВерсий();
	УстановленныеВерсии.Обновить();

КонецПроцедуры

Процедура ПриНажатииДоступныеВерсииУстановить() Экспорт
	
	Алиас = ДоступныеВерсии.ТекущиеДанные.Алиас;

	УстановщикOneScript = Новый УстановщикOneScript();
	УстановщикOneScript.УстановитьOneScript(Алиас);
	
КонецПроцедуры

#КонецОбласти

Процедура ПриСозданииОбъекта() Экспорт
	УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
	Форма = УправляемыйИнтерфейс.СоздатьФорму();

	Страницы = Форма.Элементы.Добавить("Страницы", "ГруппаФормы", Неопределено);
	Страницы.Вид = Форма.ВидГруппыФормы.Страницы;
	Страницы.Высота = 1000;

	ВерсииOneScript = Новый ВерсииOneScript();

	ТаблицаУстановленныеВерсии = Новый Провайдер;
	ТаблицаДоступныеВерсии = Новый Провайдер;
КонецПроцедуры

Процедура ОтобразитьСписокУстановленныхВерсий()

	ТаблицаУстановленныеВерсии.Источник = ВерсииOneScript.ПолучитьСписокУстановленныхВерсий();
	
	СтраницаУстановленныеВерсии = Форма.Элементы.Добавить("СтраницаУстановленныеВерсии", "ГруппаФормы", Форма.Элементы.Найти("Страницы"));
	СтраницаУстановленныеВерсии.Вид = Форма.ВидГруппыФормы.Страница;
	СтраницаУстановленныеВерсии.Заголовок = "Установленные версии";

	УстановленныеВерсии = Форма.Элементы.Добавить("УстановленныеВерсии", "ТаблицаФормы", СтраницаУстановленныеВерсии);
	УстановленныеВерсии.ПутьКДанным = ТаблицаУстановленныеВерсии;
	УстановленныеВерсии.ПоложениеЗаголовка = УправляемыйИнтерфейс.ПоложениеЗаголовка.Нет;

	КнопкаУстановленныеВерсииОбновить = Форма.Элементы.Добавить("КнопкаУстановленныеВерсииОбновить", "КнопкаФормы", СтраницаУстановленныеВерсии);
	КнопкаУстановленныеВерсииОбновить.Заголовок = "Обновить";
	КнопкаУстановленныеВерсииОбновить.УстановитьДействие(ЭтотОбъект, "Нажатие", "ПриНажатииУстановленныеВерсииОбновить");
	
КонецПроцедуры

Процедура ОтобразитьСписокДоступныхВерсий()

	ТаблицаДоступныеВерсии.Источник = ВерсииOneScript.ПолучитьСписокДоступныхКУстановкеВерсий();
	
	СтраницаДоступныеВерсии = Форма.Элементы.Добавить("СтраницаДоступныеВерсии", "ГруппаФормы", Форма.Элементы.Найти("Страницы"));
	СтраницаДоступныеВерсии.Вид = Форма.ВидГруппыФормы.Страница;
	СтраницаДоступныеВерсии.Заголовок = "Доступные версии";

	ДоступныеВерсии = Форма.Элементы.Добавить("ДоступныеВерсии", "ТаблицаФормы", СтраницаДоступныеВерсии);
	ДоступныеВерсии.ПутьКДанным = ТаблицаДоступныеВерсии;
	ДоступныеВерсии.ПоложениеЗаголовка = УправляемыйИнтерфейс.ПоложениеЗаголовка.Нет;

	КнопкаДоступныеВерсииУстановить = Форма.Элементы.Добавить("КнопкаДоступныеВерсииУстановить", "КнопкаФормы", СтраницаДоступныеВерсии);
	КнопкаДоступныеВерсииУстановить.Заголовок = "Установить";
	КнопкаДоступныеВерсииУстановить.УстановитьДействие(ЭтотОбъект, "Нажатие", "ПриНажатииДоступныеВерсииУстановить");

КонецПроцедуры
